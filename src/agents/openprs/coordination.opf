;---------------------------------------------------------------------------
;  coordination.opf - OpenPRS OP file for RCll2017 openprs-agent
;
;  Created: Tue Jan 17 15:43:00 2017
;  Copyright  2017  Mostafa Gomaa [gomaa@kbsg.rwth-aachen.de]
;  Licensed under GPLv2+ license, cf. LICENSE file
;---------------------------------------------------------------------------
(

	(defop co--Master-Achieving-workpiece-Goal
		:invocation (! (achieve-workpiece $agent $workpiece))
		:context ( 	(master-of REASONING $me)
					(agent-name $me)
					)		
		:body (	

				;(^ (precond)
				 (! (= (workpiece $id $base $rings $cap $place)  $workpiece ))
				; (if (? (~ (BOUNDP $id)))
				; 	(! (= (workpiece 0 $base $rings $cap $place)  $workpiece-to-send))
				; 	)
				;Something very very wired was happening here. Sometimes the $agent 
				;(even though it is bound Before) Gets rebound here SOMEtimes on wired
				;times. I do not get why..but one time Even a SUCCEEDED Achieve-WP with 
				;C-BS as an achiever cozed this op to Just succeeded (R-2) was the caller
				;and a C-BS ended up being an IDLE agent since everything was considered 
				;fine  

				(^ (~ (achieve-workpiece (val $agent) @workpiece @status)))

				(! (printf (FORMAT "Achievement Monitor of %s REQUESTING ...\n" $agent)))
				(=> (achieve-workpiece $agent $workpiece REQUESTING))
				

				(^ ( ||	(achieve-workpiece (val $agent) $workpiece STARTED)
						(achieve-workpiece (val $agent) $workpiece FAILED)
						(achieve-workpiece (val $agent) (workpiece @id $base $rings $cap $place)  SUCCEEDED)))
				;TODO: or timeout if no heartbeat for agent

				(if (? (achieve-workpiece (val $agent) $workpiece STARTED))
					(! (printf (FORMAT "Achievement Monitor of %s STARTED ...\n" $agent)))
					)

				(^ ( ||	(achieve-workpiece (val $agent) $workpiece FAILED)
						(achieve-workpiece (val $agent) (workpiece @id $base $rings $cap $place)  SUCCEEDED)))

				(if (? 	(achieve-workpiece (val $agent) (workpiece @id $base $rings $cap $place)  SUCCEEDED))	
					(! (printf (FORMAT "Achievement Monitor of %s SUCCEEDED ...\n" $agent)))
					GOTO fin
					) 

				(if (? 	(achieve-workpiece (val $agent) $workpiece FAILED))
					(! (printf  (FORMAT "Achievement Monitor of %s : FAILED ... \n" $agent)))
					(~> (achieve-workpiece (val $agent) $workpiece REQUESTING))
					(~> (achieve-workpiece (val $agent) $workpiece STARTED))
					(! (= 0 1));just fail the goal
					)
				LABEL fin
				
			)
		:effects ( 
					)
		:documentation "Access point to the coordination by Master of reasoning Ops. Reacts on a posted ACHIEVE-WORKPIECE goal 
						(agent and workpiece are paramters of the goal). Issues the Achieve-Workpiece request (by asserting the
						corresponding fact). Waits till achieve-workpiece is either SUCCEEDED of FAILED. ie, blocks till the goal
						is either achieved or failed"
		)


	(defop co--on-Achieving-workpiece-Started
		:invocation (achieve-workpiece $agent (workpiece $id $base $rings $cap (place $place $feature)) STARTED) 
		:context (agent-name $agent)		
		:body (
				; (! (start-critical-section))
				; (! (kill-other-intentions))
				; (=> (achieve-workpiece $agent $workpiece STARTED))
				; (! (end-critical-section))
				(! (print "Local:: Achieving Started..."))
				
				(if (? (~ (= $id 0)))
					(! (= $completed-id $id))
					)

				; Wait for the machine Achievement to be started to be sure both start together
				(if (? (= $feature "input"))
					(^ (||	(achieve-workpiece $place (workpiece $id $out-base $out-rings $out-cap (place $place "output")) STARTED)
							(achieve-workpiece $place (workpiece (* -1 $id) $out-base $out-rings $out-cap (place $place "output")) STARTED)
							))
					)

				(if (! (workpiece $completed-id $base $rings $cap (place $place $feature))) 
					(~> (achieve-workpiece $agent (workpiece $id $base $rings $cap (place $place $feature)) STARTED))
					(=> (achieve-workpiece $agent (workpiece $completed-id $base $rings $cap (place $place $feature)) SUCCEEDED))
					;The machine achievement will succeed from the  ON_READY_AT_OUTPUT. It not my concern anymore
					else
					(=> (achieve-workpiece $agent (workpiece $id $base $rings $cap (place $place $feature)) FAILED))
					
					(if (? (achieve-workpiece $place (workpiece @id $out-base $out-rings $out-cap (place $place "output")) STARTED))
						(=> (achieve-workpiece $place (workpiece (val @id) $out-base $out-rings $out-cap (place $place "output")) FAILED))
						)
					)
			)
		:documentation " On the worker agent (slave) specified for achieving a wp. When the achieve-workpiece is started. Invokes 
						the necessary workpiece operation by posting the 'workpiece' goal carried in the achieve-workpiece fact that
						was started. updates the correct state transition for the achievement facts, depending if the workpiece goal 
						Recfails of succeeds"
		)

	(defop Co--Start-MachineAchievement-Request
		:invocation ( timer "beacon" $last-sec $last-usec $seq )
		:context ( 	(Achieve-Workpiece $achiever-robot (workpiece $id $base-in $rings-in $cap-in (place $achiever-mps "input")) STARTED)
					(Achieve-Workpiece $achiever-mps (workpiece $similar-id $base-out $rings-out $cap-out (place $achiever-mps "output")) REQUESTING)
					(|| (= $similar-id $id)
						(= $similar-id (* -1 $id)))
					(machine $achiever-mps $mps-type $mps-status $mps-prepared $mps-zone $mps-pose $mps-loaded-with)
					(agent-name $achiever-robot)
					)
		:body(
			(! (start-critical-section))
			(=> (Achieve-Workpiece $achiever-mps (workpiece $similar-id $base-out $rings-out $cap-out (place $achiever-mps "output")) STARTED))
			(! (end-critical-section))
			)	
		)



	;===========================================///////// AchieveMessage Handling \\\\\\\\\\======================================

	(defop co--forAll--receive-AchieveMessage
		:invocation (protobuf-msg "llsf_msgs.AchieveWorkpiece" $comp-id $msg-type $rcvd-via $rcvd-at-sec $rcvd-at-usec		                        
															$rcv-from-host $rcvd-from-port $client-type $client-id $msg)
		:context 	(agent-name $my-name)
		:body(

			(! (= $agent 					(pb-field-value $msg 		"agent" )) )
			(! (= $status 					(pb-field-value $msg 		"status")) )
			(if (? (pb-has-field 							$msg 		"sender_info"))
				(! (= $sender-info-msg 		(pb-field-value $msg 		"sender_info")) )
				(! (= $sender-name 			(pb-field-value $sender-info-msg	"name")) )
				(! (= $sender-role 			(pb-field-value $sender-info-msg	"reasoning_role")) )			
				)
			(! (= $wp-msg 					(pb-field-value $msg 		"workpiece")) )
			(if (? (pb-has-field 							$wp-msg 	"base_color"))
				(! (= $base-color 			(pb-field-value $wp-msg 	"base_color")))
				)
			(! (= $place-msg 				(pb-field-value $wp-msg 	"place")) )
			(! (= $name 					(pb-field-value $place-msg 	"name")) )
			(! (= $feature		 			(pb-field-value $place-msg 	"feature")) )
			(if (? 	(pb-has-field 	$place-msg 	"gate"))
				(! (= $gate 				(pb-field-value $place-msg  "gate"))) 
				)
			(if (? (pb-has-field 							$wp-msg 	"id"))
				(! (= $wp-id 				(pb-field-value $wp-msg  	"id"))) 
				)
			(if (? (pb-has-field 							$wp-msg 	"first_ring_color"))
				(! (= $first-ring-color 	(pb-field-value $wp-msg 	"first_ring_color")))
				)
			(if (? (pb-has-field 							$wp-msg 	"second_ring_color"))
				(! (= $second-ring-color 	(pb-field-value $wp-msg 	"second_ring_color")))
				)
			(if (? (pb-has-field 							$wp-msg 	"third_ring_color"))
				(! (= $third-ring-color 	(pb-field-value $wp-msg 	"third_ring_color")))
				)
			(if (? (pb-has-field 							$wp-msg 	"cap_color"))
				(! (= $cap-color 			(pb-field-value $wp-msg 	"cap_color")))
				)

			(if (? (BOUNDP $first-ring-color) 	)	(! (= @rings (rings $first-ring-color))) )
			(if (? (BOUNDP $second-ring-color) 	)	(! (= @rings (rings $first-ring-color $second-ring-color))) )
			(if (? (BOUNDP $third-ring-color) 	)	(! (= @rings (rings $first-ring-color $second-ring-color $third-ring-color))) )
			(if (? (~ (BOUNDP $first-ring-color)))	(! (= @rings (rings NONE))))
			(if (? (~ (BOUNDP $base-color)) 	)	(! (= $base-color NONE)))
			(if (? (~ (BOUNDP $cap-color)) 		)	(! (= $cap-color NONE)))
			(if (? (~ (BOUNDP $gate))			)	
				(! (= @feature $feature))
				else
				(! (= @feature $gate))
				)
			;matching step
			(! (= (workpiece $wp-id (base $base-color) (val @rings) (cap $cap-color) (place $name (val @feature))) $workpiece)) 

			; (! (printf (FORMAT "RECEIVED achieve-workpiece with status %s \n" $status )))
			
			;Only Allow FINALIZE or SUCCEEDED of other achievers. OR ALL for machine achievers (unless ur the master. allow All) 
			(if  (? (& 	(~ (= $my-name $agent))
						(~ (master-of REASONING $my-name))
						(~ (= $status SUCCEEDED))
						(~ (= $status FINALIZING))
						(~ (machine $agent @mps-type @mps-status @mps-prepared @mps-zone @mps-pose @mps-loaded-with))
						))

				GOTO fin
				)
			
			; ;Ignore if SAME workpiece already has any state in the db
			; (if (? (& 	(= $status REQUESTING)
			; 			(achieve-workpiece $agent $workpiece @any-status))) 
			; 	GOTO fin
			; 	)
			; ;Ignore if DIFFERENT workpiece is still processing
			; (if (? (& 	(= $status REQUESTING)
			; 			(~ (= $workpiece 				@some-other-workpiece))
			; 			(|| (achieve-workpiece $agent 	@some-other-workpiece REQUESTING) 
			; 				(achieve-workpiece $agent 	@some-other-workpiece STARTED)))) 
			; 	GOTO fin
			; 	)

			;Ignore IF there is ANY Achieve going on here on this agent
			(if (? (& 	(= $status REQUESTING)
						(achieve-workpiece $agent @workpiece @any-status))) 
				GOTO fin
				)

			;Ignore if SAME workpiece exists with a later status
			(if (? (& 	(= $status STARTED) 
						(|| (achieve-workpiece $agent $workpiece STARTED)
							(achieve-workpiece $agent $workpiece FAILED)
							(achieve-workpiece $agent (workpiece @id (base $base-color) (val @rings) (cap $cap-color) (place $name (val @feature))) SUCCEEDED)
							(achieve-workpiece $agent (workpiece @id (base $base-color) (val @rings) (cap $cap-color) (place $name (val @feature))) FINALIZING)))) 
				GOTO fin
				)

			;Ignore if DIFFERENT workpiece is already started [this should not happen if slave ensures a sin gle workpiece achievement at all points in time]
			(if (? (& 	(= $status STARTED)
						(~ (= $workpiece @some-other-workpiece)) 
							(achieve-workpiece $agent @some-other-workpiece STARTED)))
				(! (printf (FORMAT "Should not happen %s \n" $status )))
				(fail) 
				GOTO fin
				)
			;Ignore if SAME workpiece is already finalized
			(if (? (& 	(= $status SUCCEEDED) 
							(achieve-workpiece $agent $workpiece FINALIZING)
							)) 
				GOTO fin
				)

			(if (? (& 	(= $status FAILED) 
						(||	(achieve-workpiece $agent $workpiece FAILED)
							(achieve-workpiece $agent $workpiece FINALIZING)
							))) 
				GOTO fin
				)

			; (if (? (& 	(= $status FINALIZING) 
			; 			(achieve-workpiece $agent @workpiece @stat)
			; 			(achieve-workpiece $agent @workpiece @some-status))) 
			; 	GOTO fin
			; 	)

			(=> (achieve-workpiece $agent $workpiece $status))

			LABEL fin

			;Master keeps track who sender of the success msg to make sure who knows and when to finalize and update his own world model
			(if (? (& 	(master-of REASONING $my-name)
						(BOUNDP $sender-info-msg)
						(= $status SUCCEEDED)
						))
						(=> (achievement-acknowledgement $agent $workpiece (acknowledged-by $sender-name $sender-role)))
				)
			)
		)

	;///////////////////////
	;on MASTER-OF REASONING
	;//////////////////////
    (defop co--Master--process-WorkpieceAchieve
    	:invocation ( timer "beacon" $last-sec $last-usec $seq )
    	:context	( 	(master-of REASONING $robot-name)
    	 				(peer-id private $peer-id)
    	 				)
    	:setting	(setting "robot-name" $robot-name)
    	:body(

    		(! (= $SUCC-FAIL-achievments-list (n-all-list 	(. $agent $workpiece $status .) 
    														(& 	(achieve-workpiece $agent $workpiece $status)
    														(|| (= $status SUCCEEDED) 
    															(= $status FAILED))))))
    		(if (? (< 0 (length $SUCC-FAIL-achievments-list) ))

	    		(! (= @agent-list 		(car $SUCC-FAIL-achievments-list)) )
	    		(! (= @workpiece-list 	(cadr $SUCC-FAIL-achievments-list)) )
	    		(! (= @status-list	 	(caddr $SUCC-FAIL-achievments-list)) )
	    		(while (? (~ (null @agent-list)))
	    			(! (= @agent 			(car @agent-list)))
	    			(! (= @status 			(car @status-list)))
	    			(! (= @workpiece 		(car @workpiece-list)))
	    			(! (= @agent-list 		(cdr @agent-list)))
	    			(! (= @status-list 		(cdr @status-list)))
	    			(! (= @workpiece-list	(cdr @workpiece-list))) 
	    			(if (? (= (val @status) SUCCEEDED) )
	    				; (? (= (workpiece @id @base @rings @cap @place)  (val @workpiece) )) 
	    				; (=> (workpiece (val @id) (val @base) (val @rings) (val @cap) (val @place)))
	    				;Moved to Seperate Op
	    				)
	    			
	    			;Replaced by the new finalization 
	    			; (if (? (~ (= $robot-name (val @agent))))
	    			; 	(=> (achieve-workpiece (val @agent) (val @workpiece) FINALIZING))
	    			; 	)


	    			;delete it if its still there
	    			;(~> (achieve-workpiece (val @agent) (val @workpiece) (val @status)))
	    			(!  (= (workpiece @potentially-unbound-id @base @rings @cap @place) (val @workpiece) ))
	    			(~> (achieve-workpiece $agent (workpiece @any-matching-bound-id (val @base) (val @rings) (val @cap) (val @place)) FAILED )) 
	    			(~> (achieve-workpiece $agent (workpiece @any-matching-bound-id (val @base) (val @rings) (val @cap) (val @place)) STARTED))
	    			(~> (achieve-workpiece $agent (workpiece @any-matching-bound-id (val @base) (val @rings) (val @cap) (val @place)) REQUESTING))
	    			; (~> (achieve-workpiece $agent (workpiece @any-matching-bound-id (val @base) (val @rings) (val @cap) (val @place)) SUCCEEDED))
	    			)
    			)


    		(! (= $REQ-FIN-list (n-all-list (. $agent $workpiece $status .) (& 	(achieve-workpiece $agent $workpiece $status)
    																			(|| (= $status REQUESTING) 
    																				(= $status FINALIZING))))))
    		; (! (print $REQ-FIN-list))
    		(if (? (< 0 (length $REQ-FIN-list) ))

	    		(! (= @agent-list 		(car $REQ-FIN-list)) )
	    		(! (= @workpiece-list 	(cadr $REQ-FIN-list)) )
	    		(! (= @status-list	 	(caddr $REQ-FIN-list)) )
	    		(while (? (~ (null @agent-list)))
	    			(! (= @agent 			(car @agent-list)))
	    			(! (= @status 			(car @status-list)))
	    			(! (= @workpiece 		(car @workpiece-list)))
	    			(! (= @agent-list 		(cdr @agent-list)))
	    			(! (= @status-list 		(cdr @status-list)))
	    			(! (= @workpiece-list	(cdr @workpiece-list))) 
	    			;matching step
					(! (=  (workpiece @id (base @base-color) @rings (cap @cap-color) (place @name @feature)) (val @workpiece))) 

					(! (= @place-msg (pb-create "llsf_msgs.Place")))
					(! (pb-set-field @place-msg "name" 		(val @name)))
					;If it was DS . Assigne Gate number in the msg
					(if (? (NUMBERP (val @feature)))
						(! (pb-set-field @place-msg "feature" 	"gate" ))
						(! (pb-set-field @place-msg "gate" 		(val @feature) ))
						else
						(! (pb-set-field @place-msg "feature" 	(val @feature)))
						)
					
					; (! (pb-broadcast $peer-id (val @place-msg)))
					(! (= @wp-msg (pb-create "llsf_msgs.Workpiece")))
					(! (pb-set-field @wp-msg "id" 			(val @id)))
					(if (? ( ~ (= (val @base-color) NONE)))
						(! (pb-set-field @wp-msg "base_color" (val @base-color)))
						)
					(if (? (= (val @rings) (rings NONE)))
					;Add Nothing
					else
					(if (? (= (val @rings) (rings @first-ring-color)))
						(! (pb-set-field @wp-msg "first_ring_color" (val @first-ring-color)))
						else 
						(if (? (= (val @rings) (rings @first-ring-color @second-ring-color)))
							(! (pb-set-field @wp-msg "first_ring_color" 	(val @first-ring-color)))
							(! (pb-set-field @wp-msg "second_ring_color" 	(val @second-ring-color)))
							else 
							(if (? (= (val @rings) (rings @first-ring-color @second-ring-color @third-ring-color)))
								(! (pb-set-field @wp-msg "first_ring_color" 	(val @first-ring-color)))
								(! (pb-set-field @wp-msg "second_ring_color" 	(val @second-ring-color)))
								(! (pb-set-field @wp-msg "third_ring_color" 	(val @third-ring-color)))
								)
							)
						)
					)	
					(if (? (~ (= (val @cap-color) NONE)))
						(! (pb-set-field @wp-msg "cap_color" (val @cap-color)))
					)
					(! (pb-set-field @wp-msg "place" 			(val @place-msg)))
					; (! (pb-broadcast $peer-id (val @wp-msg)))
					(! (= @msg (pb-create "llsf_msgs.AchieveWorkpiece")))
					(! (pb-set-field @msg "agent" 		(val @agent)))
					(! (pb-set-field @msg "status" 		(val @status)))
					(! (pb-set-field @msg "workpiece" 	(val @wp-msg)))
					(! (pb-broadcast $peer-id (val @msg)))
					(! (pb-destroy @msg))
					
					;Note: this does not work for somereason. Check why 
					; (! (pb-broadcast $peer-id (val @wp-msg)))
					; (! (pb-set-field @wp-msg "ring_colors" 	[ (l-list RING_BLUE)| RINGBLUE | (.RING_BLUE. ) ] ))  Somthing is not working with the pb-add-list..CANT get to use it 
					; (! (print "RING LIST"))
					; (! (print (pb-field-list @wp-msg "ring_colors")))

					;Only send finalizing once
					(if (? (= (val @status) FINALIZING) ) 
						(~> (achieve-workpiece (val @agent) (val @workpiece) FINALIZING))
						)    				
	    			)
    			)
    		)
		)

	(defop co--Master--Finalize-acknowledged-Succeeded-achievement
		:invocation (timer "beacon" $last-sec $last-usec $seq)
		:context ( 	(master-of REASONING $robot-name)
					(achieve-workpiece $achiever-agent $workpiece SUCCEEDED)
					(= $achievement-list (n-all-list (. $sender-name $role .)	 (achievement-acknowledgement $achiever-agent $workpiece (acknowledged-by $sender-name $role))))
					(= $alive-list (n-all-list (. $alive-agent .) 	(& 	(heart-beat $alive-agent @robot-number @team-name @team-color @sec @nsec)
																		(~ (= $alive-agent "RefBox")))))
					(= $differnce-list (list-difference (car $alive-list) (car $achievement-list)))
					(= 0 (length $differnce-list))
					)
		:setting (setting "robot-name" $robot-name)
		:body (
			(=> (achieve-workpiece $achiever-agent $workpiece FINALIZING))
			(! (Assert-WP-if-legal $workpiece))
			)
		:documentation "Master Finalizes the Achieved workpiece and updates the master world model.
						Happens When each live Robot has echoed the success of the Achieve-workpiece 
						(hence already updated their world models)"
		)

	(defop co--Master--Finalize-faild-achievement
		:invocation ( achieve-workpiece $achiever-agent $workpiece FAILED)
		:context 	( (master-of REASONING $robot-name) )
		:setting 	(setting "robot-name" $robot-name)
		:body 		(
						(=> (achieve-workpiece $achiever-agent $workpiece FINALIZING))
					)
		:documentation "Master Finalizes the Achieved workpiece if faild recieved "
		)

   (defop co--Slave--On-Succeeded-achievement
   		:invocation	( achieve-workpiece $some-agent $workpiece SUCCEEDED)
   		:context ( 	(agent-name $my-name)
   					(~ (master-of REASONING $my-name))
   					)
   		:body 		(
   			(! (Assert-WP-if-legal $workpiece))
   			)
  		 :documentation "On achieve-workpiece SUCCEEDED, If there is not such workpiece just assert it. 
	  					if such a workpiece exists somewhere, Only assert if it is a legal transition"
   	)

	(defop Util--WP--Assert-Legal-Transition?
		:invocation ( ! (Assert-WP-if-legal (workpiece $id $base $rings $cap (place $new-name $new-feature))) ) ;Could be better a BE maybe?
		:context ()
		:body (
			; Legal Transitions
   			;	GRIPPER==> INPUT
   			; 	GRIPPER==> SILDE
   			; 	INPUT==> GRIPPER
   			; 	INPUT==> OUTPUT
   			; 	OUTPUT==>GRIPPER
   			; 	anywhere==> LOST
   			;	input==> Gate
   			;	Gripper==> Gate

			(! (start-critical-section))

			(if (? (workpiece $id $base $rings $cap (place $new-name $new-feature)))
				GOTO SKIP
				)
			
   			(if (?  (workpiece $id @base @rings @cap (place $old-name $old-feature)))
   				(if (? (||
   							(& (= $old-feature "input") (= $new-feature "output"))
   							(& (= $old-feature "input") (= $new-feature "gripper"))
   							(& (= $old-feature "output") (= $new-feature "gripper"))
   							(= $old-feature "gripper")
   							(= $new-feature "slide")
   							(= $new-feature "consumed")
   							(= $new-feature "LOST")
   							(NUMBERP $new-feature)
   							))
   							; (& (= $old-feature "gripper") (= $new-feature "slide"))
   							; (& (= $old-feature "gripper") (= $new-feature "input"))
   					GOTO LEGAL
   					)
   				GOTO ILLEGAL
   				else
   				;Nothing with same id exists
   				GOTO LEGAL
   				)

   			LABEL ILLEGAL
   			(! (printf (FORMAT "WRONG Workpiece Transitions from %s to %s \n" $old-feature $new-feature)))
   			(! (printf (FORMAT "FAILED ASSERTING WP  from %s to %s" $old-feature $new-feature)))
   			GOTO SKIP

   			LABEL LEGAL
   			(=> (workpiece $id $base $rings $cap (place $new-name $new-feature)))
   			
   			LABEL SKIP
   			(! (end-critical-section))
			
			)
		)
	
	;///////////////////////////////////////
	;WORKER ROBOTS (Slaves or Achieving Bots) 
	;///////////////////////////////////////
	(defop co--Slave--Process-My-Achievements
		:invocation (timer "beacon" $last-sec $last-usec $seq )
		:context	(& 	(agent-name $agent)
						(peer-id private $peer-id)
						)
		:body(
			;All facts for this agent
			(! (= $achieve-workpiece-list (n-all-list 	(. $workpiece $status .)
														(achieve-workpiece $agent $workpiece $status))))
				; (! (print $achieve-workpiece-list))
			(if (? (< 0 (length $achieve-workpiece-list) ))

	    		(! (= @workpiece-list 	(car $achieve-workpiece-list)) ) 
	    		(! (= @status-list	 	(cadr $achieve-workpiece-list)) )
	    		(while (? (~ (null @status-list)))
	    			(! (= @status 			(car @status-list)))
	    			(! (= @workpiece 		(car @workpiece-list)))
	    			(! (= @status-list 		(cdr @status-list)))
	    			(! (= @workpiece-list	(cdr @workpiece-list))) 
	    		
	    			(if (? (= (val @status) REQUESTING) )
	    				;Fail if a differnt WP requested or started.
	    				(if (? (& 	(|| (achieve-workpiece $agent @differnt-workpiece STARTED)
	    								(achieve-workpiece $agent @differnt-workpiece REQUESTING))
	    							(~ (= (val @workpiece) @differnt-workpiece))))
							(! (printf (FORMAT "Trying to Achieve WP while something else is STARTED or REQUESTED  \n" )))
	    					(=> (achieve-workpiece $agent (val @workpiece) FAILED))
	    					else
		    				;Ignore If the same wp is already started.
		    				(if (? (~ (achieve-workpiece $agent (val @workpiece) STATRED)))
		    					(if (? (val @workpiece))
		    						(=> (achieve-workpiece $agent (val @workpiece) SUCCEEDED))
		    						else 
			    					(=> (achieve-workpiece $agent (val @workpiece) STARTED))
		    						; (! (= $Achieve-Goal (BUILD-GOAL 
		    						; 						(! (achieve-workpiece $agent (val @workpiece)))
		    						; 						)))
		    						; (! (INTENDED-GOAL $Achieve-Goal))
		    						)
		    					)
		    				)
	    				)

	    			(if (? (= (val @status) FINALIZING))
    					(!	(= (workpiece @potentially-unbound-id @base @rings @cap @place) (val @workpiece) ))

    					(~> (achieve-workpiece $agent (workpiece @any-matching-bound-id (val @base) (val @rings) (val @cap) (val @place)) FINALIZING))
    					(~> (achieve-workpiece $agent (workpiece @any-matching-bound-id (val @base) (val @rings) (val @cap) (val @place)) FAILED )) 
    					(~> (achieve-workpiece $agent (workpiece @any-matching-bound-id (val @base) (val @rings) (val @cap) (val @place)) STARTED))
    					(~> (achieve-workpiece $agent (workpiece @any-matching-bound-id (val @base) (val @rings) (val @cap) (val @place)) SUCCEEDED))
    					(~> (achieve-workpiece $agent (workpiece @any-matching-bound-id (val @base) (val @rings) (val @cap) (val @place)) REQUESTING))
    					)

	    			(if (? (|| 	(= (val @status) SUCCEEDED) 
	    						(= (val @status) FAILED) 
	    						(= (val @status) STARTED)))
	    				;Send it,if still in Database.
	    				(if (?  (achieve-workpiece $agent (val @workpiece) (val @status) )) 
							(! (= (workpiece @id (base @base-color) @rings (cap @cap-color) (place @name @feature)) (val @workpiece) )) 
							(! (= @place-msg (pb-create "llsf_msgs.Place")))
							(! (pb-set-field @place-msg "name" 		(val @name)))
							;If it was DS . Assigne Gate number in the msg								
							(if (? (NUMBERP (val @feature)))
								(! (pb-set-field @place-msg "feature" 	"gate" ))
								(! (pb-set-field @place-msg "gate" 		(val @feature) ))
								else
								(! (pb-set-field @place-msg "feature" 	(val @feature)))
								)

							(! (= @wp-msg (pb-create "llsf_msgs.Workpiece")))
							(! (pb-set-field @wp-msg "id" 			(val @id)))
							(if (? ( ~ (= (val @base-color) NONE)))
								(! (pb-set-field @wp-msg "base_color" (val @base-color)))
								)
							(if (? (= (val @rings) (rings NONE)))
								;Add Nothing
								else
								(if (? (= (val @rings) (rings @first-ring-color)))
									(! (pb-set-field @wp-msg "first_ring_color" (val @first-ring-color)))
									else
									(if (? (= (val @rings) (rings @first-ring-color @second-ring-color)))
										(! (pb-set-field @wp-msg "first_ring_color" 	(val @first-ring-color)))
										(! (pb-set-field @wp-msg "second_ring_color" 	(val @second-ring-color)))
										else 
										(if (? (= (val @rings) (rings @first-ring-color @second-ring-color @third-ring-color)))
											(! (pb-set-field @wp-msg "first_ring_color" 	(val @first-ring-color)))
											(! (pb-set-field @wp-msg "second_ring_color" 	(val @second-ring-color)))
											(! (pb-set-field @wp-msg "third_ring_color" 	(val @third-ring-color)))
											)
										)
									)
								)	
							(if (? (~ (= (val @cap-color) NONE)))
								(! (pb-set-field @wp-msg "cap_color" (val @cap-color)))
								)
							(! (pb-set-field @wp-msg "place"	(val @place-msg)))

							(! (= @senderinfo-msg (pb-create "llsf_msgs.SenderInfo") ))
							(! (pb-set-field @senderinfo-msg "name" $agent))
							(if (? (master-of REQUESTING $agent))
								(! (pb-set-field @senderinfo-msg "reasoning_role" "master"))
								else
								(! (pb-set-field @senderinfo-msg "reasoning_role" "slave"))
								)
							
							(! (= @msg (pb-create "llsf_msgs.AchieveWorkpiece")))
							(! (pb-set-field @msg "agent" 		$agent))
							(! (pb-set-field @msg "status" 		(val @status)))
							(! (pb-set-field @msg "workpiece" 	(val @wp-msg)))
							(! (pb-set-field @msg "sender_info" (val @senderinfo-msg)))
							(! (pb-broadcast $peer-id (val @msg)))
							(! (pb-destroy @msg))

							;Only send failed once
							; (if (? (= (val @status) FAILED) ) 
							; 	(~> (achieve-workpiece (val @agent) (val @workpiece) FAILED))
							; 	)    				
			  				)
	    				)
					)
				)
			)
		)

	(defop co--Slave--Process-Others-Achievements
		:invocation (timer "beacon" $last-sec $last-usec $seq )
		:context	(& 	(agent-name $my-name)
						(peer-id private $peer-id)
						)
		:body(
			;Select All Achievements that are not mine
			(! (= $achievement-list (n-all-list (. $achieving-agent $workpiece $status .)
							(& (achieve-workpiece $achieving-agent $workpiece $status)
								(~ (= $my-name $achieving-agent)))
				)))

			(if (? (< 0 (length $achievement-list) ))

				(! (= @achieving-agent-list (car $achievement-list)))
				(! (= @workpiece-list 		(cadr $achievement-list)))
				(! (= @status-list 			(caddr $achievement-list)))

				(while (? (~ (null @workpiece-list)))
		    		(! (= @workpiece 		(car @workpiece-list)))
		    		(! (= @workpiece-list	(cdr @workpiece-list)))
					(! (= @status 			(car @status-list)))
		    		(! (= @status-list 		(cdr @status-list)))
		    		(! (= @achieving-agent 	(car @achieving-agent-list)))
		    		(! (= @achieving-agent-list	(cdr @achieving-agent-list)))

		    		;MACHINE AS AN ACHIEVER: handling for starting the Achievements With an mps name as an achiever
		    		; (if (? (& 	(= (val @status) REQUESTING))
		    		; 			(machine (val @achieving-agent) $mps-type $mps-status $mps-prepared $mps-zone $mps-pose $mps-loaded-with)
		    		; 			))
		    		; 	(if (achieve-workpiece $my-name (workpiece ) )

		    			
		    		; 	)

	    			(if (? (= (val @status) FINALIZING))
						;Matching step
						(?	(= (workpiece @id @base @rings @cap @place) (val @workpiece) ))

						(~> (achieve-workpiece (val @achieving-agent) (workpiece @id (val @base) (val @rings) (val @cap) (val @place)) FINALIZING))
						(~> (achieve-workpiece (val @achieving-agent) (workpiece @id (val @base) (val @rings) (val @cap) (val @place)) FAILED ))
						(~> (achieve-workpiece (val @achieving-agent) (workpiece @id (val @base) (val @rings) (val @cap) (val @place)) STARTED))
						(~> (achieve-workpiece (val @achieving-agent) (workpiece @id (val @base) (val @rings) (val @cap) (val @place)) SUCCEEDED))
						(~> (achieve-workpiece (val @achieving-agent) (workpiece @id (val @base) (val @rings) (val @cap) (val @place)) REQUESTING))
						)

	    				(if (? (|| 	(= (val @status) SUCCEEDED) 
	    							(= (val @status) FAILED) 
	    							(= (val @status) STARTED)))

		    				;Send it,if still in Database.
		    				(if (?  (achieve-workpiece (val @achieving-agent) (val @workpiece) (val @status) ))
								(! (= (workpiece @id (base @base-color) @rings (cap @cap-color) (place @name @feature)) (val @workpiece) ))
								(! (= @place-msg (pb-create "llsf_msgs.Place")))
								(! (pb-set-field @place-msg "name" 		(val @name)))
								;If it was DS . Assigne Gate number in the msg
								(if (? (NUMBERP (val @feature)))
									(! (pb-set-field @place-msg "feature" 	"gate" ))
									(! (pb-set-field @place-msg "gate" 		(val @feature) ))
									else
									(! (pb-set-field @place-msg "feature" 	(val @feature)))
									)

								(! (= @wp-msg (pb-create "llsf_msgs.Workpiece")))
								(! (pb-set-field @wp-msg "id" 			(val @id)))
								(if (? ( ~ (= (val @base-color) NONE)))
									(! (pb-set-field @wp-msg "base_color" (val @base-color)))
									)
								(if (? (= (val @rings) (rings NONE)))
									;Add Nothing
									else
									(if (? (= (val @rings) (rings @first-ring-color)))
										(! (pb-set-field @wp-msg "first_ring_color" (val @first-ring-color)))
										else
										(if (? (= (val @rings) (rings @first-ring-color @second-ring-color)))
											(! (pb-set-field @wp-msg "first_ring_color" 	(val @first-ring-color)))
											(! (pb-set-field @wp-msg "second_ring_color" 	(val @second-ring-color)))
											else 
											(if (? (= (val @rings) (rings @first-ring-color @second-ring-color @third-ring-color)))
												(! (pb-set-field @wp-msg "first_ring_color" 	(val @first-ring-color)))
												(! (pb-set-field @wp-msg "second_ring_color" 	(val @second-ring-color)))
												(! (pb-set-field @wp-msg "third_ring_color" 	(val @third-ring-color)))
												)
											)
										)
									)	
								(if (? (~ (= (val @cap-color) NONE)))
									(! (pb-set-field @wp-msg "cap_color" (val @cap-color)))
									)
								(! (pb-set-field @wp-msg "place"	(val @place-msg)))
								

								(! (= @senderinfo-msg (pb-create "llsf_msgs.SenderInfo") ))
								(! (pb-set-field @senderinfo-msg "name" $my-name))

								(if (? (master-of REQUESTING $my-name))
									(! (pb-set-field @senderinfo-msg "reasoning_role" "master"))
									else
									(! (pb-set-field @senderinfo-msg "reasoning_role" "slave"))
									)

								(! (= @msg (pb-create "llsf_msgs.AchieveWorkpiece")))
								(! (pb-set-field @msg "agent" 		(val @achieving-agent)))
								(! (pb-set-field @msg "status" 		(val @status)))
								(! (pb-set-field @msg "workpiece" 	(val @wp-msg)))
								(! (pb-set-field @msg "sender_info" (val @senderinfo-msg) ))
								(! (pb-broadcast $peer-id (val @msg)))
								(! (pb-destroy @msg))
				  				)
		    				)
					)
				)
			)
		)



	;===========================================/////////======================================




	)