% Defined programs:
% base                           The initial setup.
% transition(gt)                 The transformations of the products.
%
% In this file defined predicates:
% base(P, B)                     The base color of product P is B.
% cap(P, C, GT)                  The cap color of product P at GT is C.
% nextID(R, I, GT)               The next product ID for robot R at GT will be I.
% product(R, I)                  A product generated by R, its index is I.
% ring(P, R, C, GT)              The ring color of the R-th ring of product P at GT is C.
%
% Additionally used predicates:
% capMounted(P, C, GT)           Defined in mountCap.lp
% capStationAssignment(M, C)     Defined in facts.lp
% delivered(P, GT)               Defined in delivery.lp
% deliveryTimes(B, E)            Defined in facts.lp
% genProduct(R, I, B, GT)        Defined in some task descriptions.
% ringMounted(M, P, C, GT)       Defined in mountRing.lp
% robot(R)                       Defined in facts.lp


nextID(R, 0, T-1) :- robot(R), deliveryTimes(_, T).

#program transition(gt).
product(R, I) :- genProduct(R, I, _, gt).
{ base(product(R, I), B);
  cap( product(R, I), none, gt);
  ring(product(R, I), 1, "none", gt);
  ring(product(R, I), 2, "none", gt);
  ring(product(R, I), 3, "none", gt)
  } = 5 :- genProduct(R, I, B, gt).

cap(P, C, gt) :- cap(P, C, gt-1), not capMounted(_, P, gt), not delivered(P, gt).
cap(P, C, gt) :- capMounted(M, P, gt), capStationAssignment(M, C), not delivered(P, gt).

ring(P, R, C, gt) :- ring(P, R, C, gt-1), not ringMounted(_, P, _, gt), not delivered(P, gt).
ring(P, R, C, gt) :- ringMounted(_, P, C, gt), ring(P, R, none, gt-1), not ring(P, R-1, none, gt-1).
%ring(P, 1, C, gt) :- ringMounted(_, P, C, gt), ring(P, 1, none, gt-1).

nextID(R, I, gt) :- nextID(R, I, gt-1), not genProduct(R, _, _, gt).
nextID(R, I, gt) :- robot(R), MaxID = #max{ J:genProduct(R, J, _, gt) }, I = MaxID + 1.
