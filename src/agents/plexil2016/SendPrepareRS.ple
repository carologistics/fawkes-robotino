
/***************************************************************************
 *  SendPrepareRS.ple -  PLEXIL action to prepare an RS
 *
 *  Created: Thu Aug 23 14:42:08 2018
 *  Copyright  2018  Tim Niemueller [www.niemueller.de]
 ****************************************************************************/

/*  This program is free software; you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation; either version 2 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU Library General Public License for more details.
 *
 *  Read the full text in the LICENSE.GPL file in the doc directory.
 */

        Command print(...);
        Command pprint(...);
        Command log_debug(...);
        Command log_info(...);
        Command log_warn(...);
        Command log_error(...);

String  Command pb_create(String msg_type);
        Command pb_set_value(String msg_id, String field, Any value);
        Command pb_broadcast(Integer peer_id, String msg_id);

Integer Lookup  private_peer_id;
String  Lookup  team_color;
String  Lookup  mps_state (String);
Date    Lookup  time;

SendPrepareRS:
Concurrence
{
	In String  machine;
	In String  ring_color;

	Boolean timeout_occurred = false;

  EndCondition  Lookup(mps_state(machine)) == "PREPARED" || timeout_occurred;

	PreCondition  isKnown(Lookup(private_peer_id)) &&
	              isKnown(Lookup(team_color)) &&
	              isKnown(Lookup(mps_state(machine)));
	PostCondition ! timeout_occurred;

	SendMessage:
	{
		// we need to nest another sequence so we can repeat the inner node
		// while the outer node is continuously running in parallel to the
		// long duration action FailAfterTimeout
		{
			String msgid;

			RepeatCondition Lookup(mps_state(machine)) == "IDLE";

			msgid = pb_create("llsf_msgs.PrepareMachine");
			pb_set_value(msgid, "team_color", Lookup(team_color));
			pb_set_value(msgid, "machine", machine);
			pb_set_value(msgid, "instruction_rs.ring_color", ring_color);

			pprint("Sending prepare RS", machine, ring_color);
			pb_broadcast(Lookup(private_peer_id), msgid);

			Wait 1.0;
		}
	}

	FailAfterTimeout:
	{
		StartCondition Lookup(time, Duration("PT1.0S")) >= SendPrepareRS.EXECUTING.START + Duration("PT10.0S");
		SkipCondition  Lookup(mps_state(machine)) != "IDLE";
		log_error("SendPrepareRS timeout");
		timeout_occurred = true;
	}
}

// vim: noet:ci:pi:sts=0:sw=4:ts=4
